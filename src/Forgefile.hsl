#
#                    Copyright (C) 2020, 2021 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#
#

include ~/toolsets/null/null.hsl
include ~/toolsets/utils/forge_projects.hsl
include build/installer.hsl

var exit_code type int;

project zacarias : toolset "no-tool-any-set" : $exit_code;

zacarias.prologue() {
    var supp_platforms type list;
    $supp_platforms = hefesto.sys.lines_from_file("SUPPORTED_PLATFORMS", hefesto.sys.os_name());
    if ($supp_platforms.count() == 0) {
        hefesto.sys.echo("ERROR: " + hefesto.sys.os_name() + " is not currently supported.\n" + 
                         "       If you think it should be what about opening an issue asking for " +
                         "at <https://github.com/rafael-santiago/zacarias/issues>.\n");
        hefesto.project.abort(1);
    }
    hefesto.sys.setenv("ZACARIAS_ROOTDIR", hefesto.sys.pwd());
    installer();
    var submodules type list;
    $submodules = hefesto.sys.get_option("modules");

    if ($submodules.count() == 0) {
        $submodules.add_item("libc");
        $submodules.add_item("kbd");
        $submodules.add_item("dev/ctx");
        $submodules.add_item("dev/sec");
        $submodules.add_item("dev");
        $submodules.add_item("test/kernel");
        $submodules.add_item("cmd");
    }

    $exit_code = forge_projects($submodules);
}

zacarias.epilogue() {
    if ($exit_code == 0) {
        hefesto.sys.echo("INFO: Build success.\n");
    } else {
        hefesto.sys.echo("ERROR: Build has failed.\n");
    }
}
