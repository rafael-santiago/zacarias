#
#                    Copyright (C) 2020, 2021 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#
#

include ~/fsutil.hsl
include build/toolsets.hsl

function installer() : result type none {
    var option type list;
    var opts type list;
    $opts.add_item("y");
    $opts.add_item("n");
    $opts.add_item("c");
    $opts.add_item("Y");
    $opts.add_item("N");
    $opts.add_item("C");
    var choice type string;

    $option = hefesto.sys.get_option("install");
    if ($option.count() > 0) {
        $choice = prompt("Do you want to install Zacarias on your system? [y/n/c] ", $opts);
        if ($choice == "y" || $choice == "Y") {
            hefesto.project.abort(do_install());
        } else {
            hefesto.project.abort(1);
        }
    }

    $option = hefesto.sys.get_option("uninstall");
    if ($option.count() > 0) {
        $choice = prompt("Do you want to uninstall Zacarias on your system? [y/n/c] ", $opts);
        if ($choice == "y" || $choice == "Y") {
            hefesto.project.abort(do_uninstall());
        } else {
            hefesto.project.abort(1);
        }
    }
}

local function do_install() : result type int {
    var err type int;
    $err = 1;

    var src_artifacts type list;
    var dest_artifacts type list;

    if (hefesto.sys.os_name() == "linux" || hefesto.sys.os_name() == "freebsd" || hefesto.sys.os_name() == "windows") {
        if (hefesto.sys.os_name() == "linux" || hefesto.sys.os_name() == "freebsd") {
            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "dev/zacarias.ko"));
            $dest_artifacts.add_item("/usr/local/bin/zacarias.ko");

            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "../bin/zc"));
            $dest_artifacts.add_item("/usr/local/bin/zc");

            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "../doc/MANUAL.md"));
            $dest_artifacts.add_item("/usr/local/share/zacarias/doc/MANUAL.md");

            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "../doc/man/zacarias.1"));
            $dest_artifacts.add_item("/usr/local/man/man1/zacarias.1");
        } else if (hefesto.sys.os_name() == "windows") {
            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "dev\\zacarias.sys"));
            $dest_artifacts.add_item("C:\\zacarias\\sys\\zacarias.sys");

            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "..\\bin\\zc.exe"));
            $dest_artifacts.add_item("C:\\zacarias\\bin\\zc.exe");

            $src_artifacts.add_item(hefesto.sys.make_path(zacarias_rootdir(), "..\\doc\\MANUAL.md"));
            $dest_artifacts.add_item("c:\\doc\\MANUAL.md");
        }

        $err = do_install_cp_files($dest_artifacts, $src_artifacts);
        if ($err == 0) {
            if (hefesto.sys.os_name() == "windows") {
                export_zc_path();
            }
            hefesto.sys.echo("INFO: Installed.\n");
        } else {
            # INFO(Rafael): Somethig went wrong. Try to rollback as much as possible.
            do_uninstall();
        }
    } else {
        hefesto.sys.echo("ERROR: Unsupported operating system!\n");
    }

    result $err;
}

local function export_zc_path() : result type none {
    var path type string;
    $path = hefesto.sys.env("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH");
    if ($path.len() > 0) {
        if ($path.match("C:\\\\zacarias\\\\bin") == 0) {
            if ($path.at($path.len() - 1) != ";") {
                $path = $path + ";";
            }
            $path = $path + "C:\\zacarias\\bin;";
            if (hefesto.sys.setenv("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH", $path) != 0) {
                hefesto.sys.echo("WARN: Unable to export zc path. Try to do it yourself.\n");
            }
        }
    } else {
        hefesto.sys.echo("WARN: Unable to export zc path. Try to do it yourself.\n");
    }
}

local function unexport_zc_path() : result type none {
    var str_path type string;
    $str_path = hefesto.sys.env("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH");
    if ($str_path.len() > 0) {
        var path type list;
        $path = path_env_to_list($str_path);
        var erased type int;
        $erased = 0;
        var p type int;
        $p = 0;
        $str_path = "";
        while ($p < $path.count()) {
            if ($path.item($p) != "C:\\zacarias\\bin") {
                $str_path = $str_path + $path.item($p) + ";";
            } else {
                $erased = 1;
            }
            $p = $p + 1;
        }
        if ($erased != 0) {
            if (hefesto.sys.setenv("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH", $str_path) != 0) {
                hefesto.sys.echo("WARN: Unable to unexport zc path. Try to do it yourself.\n");
            }
        }
    }
}

local function path_env_to_list(path type string) : result type list {
    var item type string;
    var p type int;
    var path_lst type list;
    $p = 0;
    while ($p < $path.len()) {
        if ($path.at($p) == ";" && $item.len() > 0) {
            $path_lst.add_item($item);
            $item = "";
        } else {
            if ($path.at($p) != ";") $item = $item + $path.at($p);
        }
        $p = $p + 1;
    }
    if ($item.len() > 0) {
        $path_lst.item($item);
    }
    result $path_lst;
}

local function do_install_cp_files(dest type list, src type list) : result type int {
    if ($dest.count() != $src.count()) {
        hefesto.sys.echo("ERROR: Destination items count is different of Source items count.");
        result 1;
    }

    var i type int;
    $i = 0;
    while ($i < $src.count()) {
        var src_path type string;
        $src_path = $src.item($i);
        if (isfile($src_path) == 0) {
            # INFO(Rafael): User call install from a fresh/clean repository,
            #               let's build the artifacts and then try to continue with installation process.
            var exit_code type int;
            $exit_code = hefesto.sys.run("hefesto");
            if ($exit_code != 0) {
                hefesto.sys.echo("ERROR: Unable to build artifacts to install.\n");
                result 1;
            }
            break;
        }
        $i = $i + 1;
    }

    $i = 0;
    while ($i < $src.count()) {
        var dest_path type string;
        $dest_path = $dest.item($i);
        $dest_path = pathfromfilepath($dest_path);
        if (mktree($dest_path) != 1) {
            hefesto.sys.echo("ERROR: Unable to make directory path `" + $dest_path + "`.\n");
            result 1;
        }
        if (hefesto.sys.cp($src.item($i), $dest.item($i)) != 1) {
            hefesto.sys.echo("ERROR: Unable to install `" + $dest.item($i) + "`.\n");
            result 1;
        }
        $i = $i + 1;
    }

    result 0;
}

local function do_uninstall() : result type int {
    var err type int;

    var to_burn type list;

    if (hefesto.sys.os_name() == "linux"   ||
        hefesto.sys.os_name() == "freebsd" ||
        hefesto.sys.os_name() == "windows") {
        if (hefesto.sys.os_name() == "linux" || hefesto.sys.os_name() == "freebsd") {
            $to_burn.add_item("/usr/local/bin/zacarias.ko");
            $to_burn.add_item("/usr/local/bin/zc");
            $to_burn.add_item("/usr/local/share/zacarias/doc/MANUAL.md");
            $to_burn.add_item("/usr/local/man/man1/zacarias.1");
            $to_burn.add_item("/usr/local/share/zacarias/bin");
            $to_burn.add_item("/usr/local/share/zacarias/doc");
            $to_burn.add_item("/usr/local/share/zacarias");
        } else if (hefesto.sys.os_name() == "windows") {
            $to_burn.add_item("C:\\zacarias\\sys\\zacarias.sys");
            $to_burn.add_item("C:\\zacarias\\bin\\zc.exe");
            $to_burn.add_item("C:\\zacarias\\doc\\MANUAL.md");
            $to_burn.add_item("C:\\zacarias\\sys");
            $to_burn.add_item("C:\\zacarias\\bin");
            $to_burn.add_item("C:\\zacarias\\doc");
            $to_burn.add_item("C:\\zacarias");
        }
        $err = do_uninstall_rm_paths($to_burn);
        if ($err == 0) {
            if (hefesto.sys.os_name() == "windows") {
                unexport_zc_path();
            }
            hefesto.sys.echo("INFO: Uninstalled.\n");
        }
    } else {
        hefesto.sys.echo("ERROR: Unsupported operating system!\n");
    }

    result $err;
}

local function do_uninstall_rm_paths(rmlist type list) : result type int {
    var err type int;
    $err = 0;

    var r type int;
    $r = 0;
    while ($r < $rmlist.count()) {
        var path type string;
        $path = $rmlist.item($r);
        if (isfile($path)) {
            if (hefesto.sys.rm($path) != 0) {
                hefesto.sys.echo("WARN: Unable to remove the file `" + $path +
                                 "`. Try to do it by yourself.\n");
            }
        } else if (isdir($path)) {
            if (hefesto.sys.rmdir($path) != 0) {
                hefesto.sys.echo("WARN: Unable to remove the directory `" + $path +
                                 "`. Try to do it by yourself.\n");
                $err = 1;
            }
        }
        $r = $r + 1;
    }

    result $err;
}

local function prompt(message type string, choices type list) : result type string {
    var choice type string;
    $choice = "";
    while ($choices.index_of($choice) == -1) {
        $choice = hefesto.sys.prompt($message);
    }
    result $choice;
}
